// Belli-Zard item filter, updated for P20-Perlite
// Version 0.1
//
// Based on original filter by Kryszard 
// twitch.tv/kryszard
//
// For Casual and EndGame Players

//Gold
ItemDisplay[GOLD<100 CLVL<20]:
ItemDisplay[GOLD<1000 (CLVL>19 AND CLVL<80)]:
ItemDisplay[GOLD<5000 CLVL>79]:

//Throws 35k+
ItemDisplay[(7tk OR 7bk OR 7ta) (MAG OR RARE OR NMAG) (!ETH AND !INF)]: %GRAY%throw %TAN%[35k]

//Show me number of sockets on ETH AND ID items
ItemDisplay[(ARMOR OR WEAPON) ((MAG OR RARE OR SET OR UNI) AND ETH) SOCK>0]: %NAME% %GRAY%ETH %TAN%[%SOCKETS%]
//Show me number of sockets on ID items
ItemDisplay[(ARMOR OR WEAPON) (MAG OR RARE OR SET OR UNI) SOCK>0]: %NAME% %TAN%[%SOCKETS%]

//Health & Mana Potions
ItemDisplay[rvl]: %PURPLE%R %WHITE%70%
ItemDisplay[rvs]: %PURPLE%r %WHITE%35%
ItemDisplay[hp5]: %RED%!%WHITE%Hp
ItemDisplay[mp5]: %BLUE%!%WHITE%Mp
ItemDisplay[hp1 DIFFICULTY=0]: %RED%!%WHITE%minor hp
ItemDisplay[hp2 DIFFICULTY=0]: %RED%!%WHITE%light hp
ItemDisplay[hp3 DIFFICULTY=0]: %RED%!%WHITE%healing
ItemDisplay[hp4 DIFFICULTY<2]: %RED%!%WHITE%great hp
ItemDisplay[mp1 DIFFICULTY=0]: %BLUE%!%WHITE%minor mp
ItemDisplay[mp2 DIFFICULTY=0]: %BLUE%!%WHITE%light mp
ItemDisplay[mp3 DIFFICULTY=0]: %BLUE%!%WHITE%mana
ItemDisplay[mp4 DIFFICULTY<2]: %BLUE%!%WHITE%great mp
ItemDisplay[(hp1 OR hp2 OR hp3 OR mp1 OR mp2 OR mp3) DIFFICULTY>0]:
ItemDisplay[(hp4 OR mp4) DIFFICULTY>1]:

//Utilites
ItemDisplay[tsc]: %BLUE%°%GRAY%tp
ItemDisplay[isc]: %RED%°%GRAY%id
ItemDisplay[vps CLVL<41]: %GREEN%!%WHITE%stamina
ItemDisplay[vps CLVL>40]:
ItemDisplay[wms CLVL<61]: %GREEN%!%WHITE%thaw
ItemDisplay[wms CLVL>60]:
ItemDisplay[yps]: %GREEN%!%WHITE%a
ItemDisplay[key]: %GRAY%key
//Choking, Exploding, Fulminating, Oil, Rancid, Strangling
ItemDisplay[(gpm OR opm OR opl OR ops OR gps OR gpl) CLVL>23]:

//Essences
ItemDisplay[tes]: %MAP%%NAME% %GRAY%[Andariel]
ItemDisplay[ceh]: %MAP%%NAME% %GRAY%[Mephisto]
ItemDisplay[bet]: %MAP%%NAME% %GRAY%[Diablo]
ItemDisplay[fed]: %MAP%%NAME% %GRAY%[Baal]
ItemDisplay[toa]: %MAP%%NAME%

//Uber Keys
ItemDisplay[pk1]: %MAP%%NAME% %GRAY%[Countess]
ItemDisplay[pk2]: %MAP%%NAME% %GRAY%[Summoner]
ItemDisplay[pk3]: %MAP%%NAME% %GRAY%[Nihlathak]

//PoD Items
ItemDisplay[std]: %NAME%
ItemDisplay[cx5]: %MAP%%PURPLE%»» %GOLD%» %PURPLE%»    %PURPLE%ooc    %PURPLE%« %GOLD%« %PURPLE%««
ItemDisplay[cx6]: %MAP%%PURPLE%Diablo's Soulstone
ItemDisplay[cx7]: %MAP%%RED%»»»» %NAME%%RED% ««««
ItemDisplay[ma1 OR ma2 OR ma3 OR ma4 OR ma5 OR ma6 OR ma7 OR ma8 OR ma9 OR ma10 OR maz]: %MAP%%NAME% %GRAY%[%DGREEN%~ %PURPLE%MAP %DGREEN%~%GRAY%]

// quest
ItemDisplay[j34]: %MAP%%TAN%%NAME%

//Wirt Leg
ItemDisplay[leg SOCK>0]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[leg]: %NAME%

//Runes
ItemDisplay[r01 OR r02 OR r03 OR r07 OR r10 OR r14 OR r16 OR r17]: %ORANGE%%RUNENAME% %GRAY%[%RUNENUM%]
ItemDisplay[r04 OR r05 OR r06 OR r08 OR r09 OR r11 OR r12 OR r13 OR r15]: %ORANGE%%RUNENAME% %TAN%[%RUNENUM%]
ItemDisplay[r18 OR r19 OR r20 OR r21 OR r22 OR r23]: %NOTIFY-ORANGE%%RED%» %PURPLE%» %ORANGE%  %RUNENAME% %GOLD%[%RUNENUM%]  %PURPLE% «%RED% «
ItemDisplay[r24 OR r25]: %NOTIFY-ORANGE%%NL%%NL%%RED%[%RUNENUM%] %PURPLE%»» »    %ORANGE%  %RUNENAME% %GOLD%[%RUNENUM%]    %PURPLE%« ««%RED% [%RUNENUM%]%NL%%NL%
ItemDisplay[r26 OR r27 OR r32]: %NOTIFY-PURPLE%%NL%%NL%%GREEN%[%RUNENUM%] %PURPLE%»»  »     %ORANGE%%RUNENAME% %GOLD%[%RUNENUM%]%PURPLE%     « «« %GREEN%[%RUNENUM%]%NL%%NL%
ItemDisplay[r28 OR r29 OR r33]: %NOTIFY-PURPLE%%NL%%NL%%GREEN%[%RUNENUM%] %PURPLE%»»» »»  »     %ORANGE%%RUNENAME% %GOLD%[%RUNENUM%]%PURPLE%     «   «« ««« %GREEN%[%RUNENUM%]%NL%%NL%
ItemDisplay[r30 OR r31]: %NOTIFY-PURPLE%%NL%%NL%%GREEN%[%RUNENUM%] %PURPLE%»»»» »»  »»    »     %ORANGE%%RUNENAME% %GOLD%[%RUNENUM%]%PURPLE%     «    ««  «« «««« %GREEN%[%RUNENUM%]%NL%%NL%

//Gems
ItemDisplay[(gfv OR gfw OR gfg OR gfr OR gfb OR gfy OR skf) DIFFICULTY>0]:
ItemDisplay[(gsv OR gsw OR gsg OR gsr OR gsb OR gsy OR sku) DIFFICULTY>1]:
ItemDisplay[(gcv OR gcw OR gcg OR gcr OR gcb OR gcy OR skc) CLVL<19]: %NAME%
ItemDisplay[(gcv OR gcw OR gcg OR gcr OR gcb OR gcy OR skc)]: %GRAY%chip
ItemDisplay[gzv]: %WHITE%Amethyst %ORANGE%[%PURPLE%O%ORANGE%]
ItemDisplay[glw]: %WHITE%Diamond %ORANGE%[%WHITE%O%ORANGE%]
ItemDisplay[glg]: %WHITE%Emerald %ORANGE%[%GREEN%O%ORANGE%]
ItemDisplay[glr]: %WHITE%Ruby %ORANGE%[%RED%O%ORANGE%]
ItemDisplay[glb]: %WHITE%Sapphire %ORANGE%[%BLUE%O%ORANGE%]
ItemDisplay[gly]: %WHITE%Topaz %ORANGE%[%YELLOW%O%ORANGE%]
ItemDisplay[skl]: %WHITE%Skull %ORANGE%[%GRAY%O%ORANGE%]
ItemDisplay[gpv]: %ORANGE%P %WHITE%Amethyst %ORANGE%[%PURPLE%O%ORANGE%]
ItemDisplay[gpw]: %ORANGE%P %WHITE%Diamond %ORANGE%[%WHITE%O%ORANGE%]
ItemDisplay[gpg]: %ORANGE%P %WHITE%Emerald %ORANGE%[%GREEN%O%ORANGE%]
ItemDisplay[gpr]: %ORANGE%P %WHITE%Ruby %ORANGE%[%RED%O%ORANGE%]
ItemDisplay[gpb]: %ORANGE%P %WHITE%Sapphire %ORANGE%[%BLUE%O%ORANGE%]
ItemDisplay[gpy]: %ORANGE%P %WHITE%Topaz %ORANGE%[%YELLOW%O%ORANGE%]
ItemDisplay[skz]: %ORANGE%P %WHITE%Skull %ORANGE%[%GRAY%O%ORANGE%]

//Arrows and Bolts
ItemDisplay[aqv CLVL>12]:
ItemDisplay[aq2 NMAG CLVL>12]:
ItemDisplay[aq2 MAG CLVL>40]:
ItemDisplay[cqv CLVL>12]:
ItemDisplay[cq2 NMAG CLVL>12]:
ItemDisplay[cq2 MAG CLVL>40]:

//Circlets
ItemDisplay[EQ7 MAG ETH]: %GRAY%ETH %BLUE%%NAME%
ItemDisplay[EQ7 RARE ETH]: %GRAY%ETH %YELLOW%%NAME%
ItemDisplay[EQ7 RARE !ETH !ID]: %TAN%»» %YELLOW%%NAME%%TAN% ««
ItemDisplay[EQ7 (MAG OR RARE)]: %NAME%
ItemDisplay[ci3 NMAG !ETH SOCK=0]: %NAME% %YELLOW%[%GRAY%imbue%YELLOW%]

// shoppables / special items
ItemDisplay[WEAPON TABSK34>2]: %RED%»» »» »» %GREEN%3bo weapon%RED% «« «« ««
ItemDisplay[EQ4 (TABSK0>2 OR TABSK2>2 OR TABSK50>2) AND IAS>19]: %RED%»» »» »» %GREEN%3sk-20ias Gloves%RED% «« «« ««
ItemDisplay[WP13 (TABSK24>2 AND SK119>2)]: %RED%»» »» »» %GREEN%FoH Scepter%RED% «« «« ««
ItemDisplay[WP12 MAG CHSK91>0]: %RED%»» »» »» %GREEN%Lower Resist Wand%RED% «« «« ««

//Class items
ItemDisplay[(CL1 OR CL2 OR CL5 OR CL6 OR WP12) MAG ETH]: %GRAY%ETH %BLUE%%NAME%
ItemDisplay[(CL1 OR CL2 OR CL5 OR CL6 OR WP12) RARE ETH]: %GRAY%ETH %YELLOW%%NAME%
ItemDisplay[(CL1 OR CL2 OR CL5 OR CL6 OR WP12) (MAG OR RARE)]: %NAME%
ItemDisplay[WEAPON (WP6 AND CL7) MAG ETH]: %GRAY%ETH %BLUE%%NAME%
ItemDisplay[WEAPON (WP6 AND CL7) RARE ETH]: %GRAY%ETH %YELLOW%%NAME%
ItemDisplay[WEAPON (WP6 AND CL7) (MAG OR RARE)]: %NAME%
ItemDisplay[(wsp OR 9ws OR 7ws) (MAG OR RARE) !ETH]: %NAME%

//All Charms
ItemDisplay[cm3 MAG !ID ILVL>90]: %MAP%%PURPLE%»»»%BLUE%  %NAME%  %ORANGE%[%WHITE%%ILVL%%ORANGE%]
ItemDisplay[cm1 MAG !ID]: %MAP%%GREEN%»%BLUE%  %NAME%  %GRAY%[%ILVL%]
ItemDisplay[cm2 MAG !ID]: %TAN%»»%BLUE%  %NAME%  %GRAY%[%ILVL%]
ItemDisplay[cm3 MAG !ID]: %MAP%%PURPLE%»»»%BLUE%  %NAME%  %GRAY%[%ILVL%]
ItemDisplay[cm3 MAG ILVL>90]: %MAP%%NAME% %ORANGE%[%WHITE%%ILVL%%ORANGE%]
ItemDisplay[cm1 OR cm2 OR cm3]: %NAME% %GRAY%[%ILVL%]

//Rings and amulets
ItemDisplay[(rin OR amu) RARE !ID]: %MAP%%TAN%»» %YELLOW%%NAME%%TAN% ««
ItemDisplay[(rin OR amu) MAG ILVL>90]: %NAME% %WHITE%[%ILVL%]
ItemDisplay[(rin OR amu) MAG]: %NAME% %GRAY%[%ILVL%]

//Jewels
ItemDisplay[jew MAG !ID]: %MAP%%TAN%»%WHITE%» %BLUE%%NAME%%WHITE% «%TAN%«
ItemDisplay[jew RARE !ID]: %MAP%%TAN%»» %YELLOW%%NAME%%TAN% ««
ItemDisplay[UNI !ID jew]: %NOTIFY-PURPLE%%NL%%RED%»» %YELLOW%»» %GREEN%»  %BLUE%»  %PURPLE%Rainbow Facet  %BLUE%«  %GREEN%« %YELLOW%«« %RED%««%NL%

//Craftables
ItemDisplay[(ARMOR OR WEAPON) !(NMAG OR MAG OR RARE OR SET OR UNI)]: %NAME%
ItemDisplay[(uvg OR xvg) MAG]: %NAME% %RED%[nef] %GRAY%[%ILVL%]
ItemDisplay[xtb MAG]: %NAME% %RED%[eth] %GRAY%[%ILVL%]
ItemDisplay[(xlg OR ulg) MAG]: %NAME% %PURPLE%[ort] %GRAY%[%ILVL%]
ItemDisplay[(uvc OR zvb) MAG]: %NAME% %PURPLE%[ith] %GRAY%[%ILVL%]
ItemDisplay[(umg OR xmg) MAG]: %NAME% %WHITE%[%BLUE%ort%WHITE%] %GRAY%[%ILVL%]
ItemDisplay[uit MAG]: %NAME% %GREEN%[nef] %GRAY%[%ILVL%]

//Rare Gloves, Belts and Boots
ItemDisplay[(EQ4 OR EQ5 OR EQ6) ETH RARE]: %GRAY%ETH %YELLOW%%NAME%
ItemDisplay[(EQ4 OR EQ5 OR EQ6) RARE]: %NAME%

//OK UNIQUE Items ETH
ItemDisplay[(xhn OR upl OR uld OR uow OR ama) ETH UNI !ID]: %NOTIFY-ORANGE%%YELLOW%»%TAN%»%GRAY%ETH»     %GOLD%%NAME%%GRAY%     «%TAN%«%YELLOW%«

//OK UNIQUE Items NONETH
ItemDisplay[(uow OR pa9 OR nea OR 7cr OR 7fl OR 7kr OR oba OR ama OR lbt OR xhb OR uhb OR xvb OR xhn OR uhm OR xh9 OR usk OR ci2 OR baa OR bac OR dra OR dre OR xhn OR xtp OR xlt OR upl OR uld OR uvg OR uhg OR zlb OR zvb OR zhb OR umc) UNI !ID !ETH]: %NOTIFY-ORANGE%%YELLOW%»%TAN%»%GRAY%»     %GOLD%%NAME%%GRAY%     «%TAN%«%YELLOW%«

//GG Uniq Items
ItemDisplay[(ulc OR xea OR uap OR ulm OR ci3 OR xtb OR 7gw OR obf OR uvb OR uar) UNI ETH !ID]: %NOTIFY-PURPLE%%NL%%YELLOW%»%TAN%»%GRAY%»     ETH %GOLD%%NAME%%GRAY%     «%TAN%«%YELLOW%«%NL%
ItemDisplay[(rin OR amu OR ulc OR xea OR urn OR uap OR ulm OR ci3 OR xtb OR uit OR 6lw OR 7gw OR 7gd OR obf OR uvb OR uar) UNI !ID]: %NOTIFY-PURPLE%%NL%%PURPLE%»»%RED%»» %ORANGE%»»  %YELLOW%»     %GOLD%%NAME%%YELLOW%     «%ORANGE%  «« %RED%««%PURPLE%««%NL%

//Good set parts
ItemDisplay[(amu OR uar OR ci3 OR 7qr OR uth OR ulg OR xtb OR xhm OR xmg) SET !ID]: %MAP%%PURPLE%»»%RED%»» %DGREEN%»»  %GREEN%»     %NAME%     «  %DGREEN%«« %RED%««%PURPLE%««

//RuneWord Bases
//Spirit Swords
ItemDisplay[(crs OR bsd OR lsd) (ETH AND (NMAG OR RW)) SOCK=4]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(crs OR bsd OR lsd) (NMAG OR RW) SOCK=4]: %NAME% %TAN%[%SOCKETS%]
//Holy Thunder/Rift
ItemDisplay[wsp !ETH AND (NMAG OR RW) SOCK=4]: %NAME% %TAN%[%SOCKETS%]
//HOTO AND CTA
ItemDisplay[fla (ETH AND (NMAG OR RW)) SOCK>3]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[fla (NMAG OR RW) SOCK>3]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[crs (ETH AND (NMAG OR RW)) SOCK=5]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[crs (NMAG OR RW) SOCK=5]: %NAME% %TAN%[%SOCKETS%]
//White
ItemDisplay[WP12 SK84>2 (SOCK=0 OR SOCK=2) (NMAG OR RW)]: %GRAY%[%WHITE%3bsr%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[WP12 SK83>2 (SOCK=0 OR SOCK=2) (NMAG OR RW)]: %GRAY%[%WHITE%3d%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[WP12 SK92>2 (SOCK=0 OR SOCK=2) (NMAG OR RW)]: %GRAY%[%WHITE%3pn%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[WP12 SK93>2 (SOCK=0 OR SOCK=2) (NMAG OR RW)]: %GRAY%[%WHITE%3bst%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Memory
ItemDisplay[(cst OR bst OR 8cs OR 8bs OR 6cs OR 6bs) (NMAG OR RW) SK58>2 (SOCK=0 OR SOCK=4)]: %GRAY%[%WHITE%3es%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(wst OR 8ws OR 6ws) (NMAG OR RW) SK58>2 SOCK=0 (!INF AND !SUP)]: %GRAY%[%WHITE%3es%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(wst OR 8ws OR 6ws) (NMAG OR RW) SK58>2 SOCK=4]: %GRAY%[%WHITE%3es%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Faith
ItemDisplay[amc (NMAG OR RW) TABSK0>2 SOCK=4]: %GRAY%[%WHITE%3bow%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[amc (NMAG OR RW) TABSK0>2 SOCK=0 (!INF AND !SUP)]: %GRAY%[%WHITE%3bow%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[6cb SOCK=4]: %NAME% %TAN%[%SOCKETS%]
//Infi/Insi/Pride and Obedience/Doom
ItemDisplay[(7wc OR 7pa OR 7s8 OR 7vo OR 7h7 OR 7tr OR 7br OR 7st OR 7p7) (ETH AND (NMAG OR RW)) (SOCK=4 OR SOCK=5)]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(7vo OR 7tr) (ETH AND !INF) AND (NMAG OR RW) SOCK=0]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(7wc OR 7pa OR 7s8 OR 7h7 OR 7br OR 7st) (ETH AND !INF AND !SUP) AND (NMAG OR RW) SOCK=0]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Grief/Beast/Passion
ItemDisplay[(7cr OR 7wa) (!ETH AND (NMAG OR RW)) (SOCK=4 OR SOCK=5)]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(7cr OR 7wa) (!ETH AND !INF AND !SUP) AND (NMAG OR RW) SOCK=0]: %GRAY%%NAME% %TAN%[%SOCKETS%]
//Death/Oath
ItemDisplay[(7wa OR 7fb OR 7gd) (ETH AND (NMAG OR RW)) (SOCK=4 OR SOCK=5)]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[7fb (ETH AND !INF) AND (NMAG OR RW) SOCK=0]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(7wa OR 7gd) (ETH AND !INF AND !SUP) AND (NMAG OR RW) SOCK=0]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Breath of the Dying/Last Wish
ItemDisplay[7cr !INF AND (NMAG OR RW) (SOCK=0 OR SOCK=6)]: %GRAY%%NAME% %TAN%[%SOCKETS%]
ItemDisplay[(7wa OR 7gd OR 7p7) (ETH AND !INF) AND (NMAG OR RW) (SOCK=0 OR SOCK=6)]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Lawbringer
ItemDisplay[72h (ETH AND !INF) AND (NMAG OR RW) (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Chaos
//ItemDisplay[9tw AND (!ETH AND !INF) AND (NMAG OR RW) AND (SK274>0 AND SK275>0) AND (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%chaos%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(7tw OR 7qr OR 7ar) (!ETH AND !INF) (NMAG OR RW) (SOCK=0 OR SOCK=3) SK263>1 (SK279>0 OR SK273>0 OR SK275>0)]: %MAP%%GRAY%[%WHITE%chaos%GRAY%] %NAME% %TAN%[%SOCKETS%]
// MA 
ItemDisplay[(7tw OR 7qr OR 7ar) (!ETH AND !INF) (NMAG OR RW) (SOCK=0 OR SOCK=3) SK274=3 (SK265>1 OR SK259>1 OR SK280>1 OR SK254>1)]: %MAP%%GRAY%[%WHITE%ma%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Plague
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) (SK277>2 AND SK278>0) (SOCK=0 OR SOCK=3)]: %MAP%%GRAY%[%PURPLE%bs+vnm%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK278>2 SK267>0 (SOCK=0 OR SOCK=3)]: %MAP%%GRAY%[%PURPLE%vnm+fade%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK277>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3bs%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) (SK261>2 OR SK262>2 OR SK263>2 OR SK264>2 OR SK265>2 OR SK266>2 OR SK267>2 OR SK268>2 OR SK269>2 OR SK270>2 OR SK271>2 OR SK272>2 OR SK273>2 OR SK274>2 OR SK275>2 OR SK276>2 OR SK278>2 OR SK279>2 OR SK280>2) SK277>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3bs+%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK278>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3vnm%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK262>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3wof%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK272>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3woi%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK271>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3ls%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK266>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3bf%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) SK276>2 (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3ds%GRAY%] %NAME% %TAN%[%SOCKETS%]
// Any nonmagical 3 skill claw
ItemDisplay[(clw OR btl OR skr OR 9ar OR 9lw OR 9tw OR 9qr OR 7ar OR 7wb OR 7lw OR 7tw OR 7qr) !INF (NMAG OR RW) (SK261>2 OR SK262>2 OR SK263>2 OR SK264>2 OR SK265>2 OR SK266>2 OR SK267>2 OR SK268>2 OR SK269>2 OR SK270>2 OR SK271>2 OR SK272>2 OR SK273>2 OR SK274>2 OR SK275>2 OR SK276>2 OR SK277>2 OR SK278>2 OR SK279>2 OR SK280>2) (SOCK=0 OR SOCK=3)]: %GRAY%[%WHITE%3Skill%GRAY%] %NAME% %TAN%[%SOCKETS%]

//Armors
ItemDisplay[xtp !ETH AND (NMAG OR RW) SOCK=2]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(xtp OR utp OR uui) (!INF AND !ETH) AND (NMAG OR RW) (SOCK=0 OR SOCK>2)]: %GRAY%%NAME% %TAN%[%SOCKETS%]
ItemDisplay[EQ2 ELT ETH AND (NMAG OR RW) (SOCK=0 OR SOCK>2)]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[EQ2 ELT ETH AND (NMAG OR RW) (SOCK=0 OR SOCK>2) AND DEF>=775]: %GRAY%[%WHITE%eth:775+%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Helmets
ItemDisplay[(ci3 OR uh9) (!INF AND !ETH) AND (NMAG OR RW) SOCK=3]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL2 ((!INF AND !ETH) AND (NMAG OR RW)) SK149>2 !SOCK=1]: %GRAY%[%WHITE%3bo%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL2 ((!INF AND !ETH) AND (NMAG OR RW)) SK154>2 !SOCK=1]: %GRAY%[%WHITE%3wc%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK221>2 !SOCK=1]: %GRAY%[%WHITE%3rvn%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK227>2 !SOCK=1]: %GRAY%[%WHITE%3swf%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK237>2 !SOCK=1]: %GRAY%[%WHITE%3dwf%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK238>2 !SOCK=1]: %GRAY%[%WHITE%3rbs%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK247>2 !SOCK=1]: %GRAY%[%WHITE%3gzy%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK250>2 !SOCK=1]: %GRAY%[%WHITE%3hur%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK249>2 !SOCK=1]: %GRAY%[%WHITE%3agn%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK245>2 !SOCK=1]: %GRAY%[%WHITE%3tno%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK244>2 !SOCK=1]: %GRAY%[%WHITE%3vno%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK240>2 !SOCK=1]: %GRAY%[%WHITE%3tsr%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK243>2 !SOCK=1]: %GRAY%[%WHITE%3swv%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK239>2 !SOCK=1]: %GRAY%[%WHITE%3fcw%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL1 ((!INF AND !ETH) AND (NMAG OR RW)) SK225>2 !SOCK=1]: %GRAY%[%WHITE%3fst%GRAY%] %NAME% %TAN%[%SOCKETS%]
//Shields
ItemDisplay[(pab OR pac) (!INF AND ETH) AND (NMAG OR RW) (SOCK=0 OR SOCK=4) RES>34]: %GRAY%[%WHITE%eth%GRAY%][%WHITE%res%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(pab OR pac) (!INF AND !ETH) AND (NMAG OR RW) (SOCK=0 OR SOCK>2) RES>34]: %GRAY%[%WHITE%res%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[CL3 (!INF AND !ETH) AND (NMAG OR RW) (SOCK=0 OR SOCK>2) (RES>34 AND CLVL<81)]: %GRAY%[%WHITE%res%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[ush (!INF AND !ETH) AND (NMAG OR RW)  SOCK>1]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[uit !ETH !RW  NMAG (SOCK=4 OR SOCK=0) !STAT91=0 !STAT91=-10]: %GRAY%[%WHITE%-% Req%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[uit !ETH NMAG (SOCK=4 OR SOCK=0) STAT91=-10]: %GRAY%[%WHITE%-10% Req%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[uit (!INF AND ETH) AND (NMAG OR RW)  (SOCK=0 OR SOCK=4)]: %GRAY%[%WHITE%eth%GRAY%] %NAME% %TAN%[%SOCKETS%]
ItemDisplay[uit (!INF AND !ETH) AND (NMAG OR RW)  (SOCK=0 OR SOCK=4)]: %GRAY%%NAME% %TAN%[%SOCKETS%]

// SHOW ME THAT UNIQ ITEM IS ETH
ItemDisplay[UNI AND ETH AND SOCK=0]: %GRAY%ETH %GOLD%%NAME%
//Show me every  uniq and set item and runewords
ItemDisplay[RW]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(UNI OR SET)]: %NAME%
//Show me every identified item
ItemDisplay[(ARMOR OR WEAPON) (MAG OR RARE OR SET OR UNI) ID]: %NAME%

//HIDE EVERYTHING ELSE
ItemDisplay[(CL1 OR CL2 OR CL4 OR CL6 OR WP11 OR WP12 OR WP13) NMAG ETH CLVL<31 SOCK>0]: %GRAY%ETH %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(CL1 OR CL2 OR CL4 OR CL6 OR WP11 OR WP12 OR WP13) NMAG ETH CLVL<31]: %GRAY%ETH %NAME%
ItemDisplay[(CL1 OR CL2 OR CL4 OR CL6 OR WP11 OR WP12 OR WP13) NMAG CLVL<31 SOCK>0]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(CL1 OR CL2 OR CL4 OR CL6 OR WP11 OR WP12 OR WP13) NMAG CLVL<31]: %NAME% 
ItemDisplay[(ARMOR OR WEAPON) MAG ETH CLVL<60]: %GRAY%ETH %BLUE%%NAME%
ItemDisplay[(ARMOR OR WEAPON) RARE ETH CLVL<60]: %GRAY%ETH %YELLOW%%NAME%
ItemDisplay[(ARMOR OR WEAPON) (MAG OR RARE) CLVL<60]: %NAME%
ItemDisplay[(ARMOR OR WEAPON) NMAG AND (!ETH AND !INF) SOCK>1 CLVL<40]: %GRAY%%NAME% %TAN%[%SOCKETS%]
ItemDisplay[WEAPON NMAG AND (ETH AND !INF) SOCK=4 CLVL<60]: %GRAY%ETH %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(ARMOR OR WEAPON) (EXC OR ELT) NMAG AND (!ETH AND !INF) SOCK>1 CLVL>39 AND CLVL<81]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(ARMOR OR WEAPON) INF]:e
ItemDisplay[(ARMOR OR WEAPON) ETH SOCK>0 CLVL<19]: %GRAY%ETH %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(ARMOR OR WEAPON) ETH CLVL<19]: %GRAY%ETH %NAME%
ItemDisplay[(ARMOR OR WEAPON) SOCK>0 CLVL<19]: %NAME% %TAN%[%SOCKETS%]
ItemDisplay[(ARMOR OR WEAPON) CLVL>18]: